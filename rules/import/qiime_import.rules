rule all_qiime_import:
    input:
        TARGET_QIIME_IMPORT

if config["all"]["paired_end"]:

    ruleorder: get_demux_qza_paired > get_demux_qza_unpaired

else:

    ruleorder: get_demux_qza_unpaired > get_demux_qza_paired

rule get_demux_qzv:
    input:
        QIIME_OUTPUT_DIR + "/demux.qza"
    output:
        QIIME_OUTPUT_DIR + "/demux.qzv"
    shell:
        """
        qiime demux summarize \
        --i-data {input} \
        --o-visualization {output}    
        """

rule get_demux_qza_paired:
    input: 
        PROJECT_DIR + "/manifest.csv"
    output:
        QIIME_OUTPUT_DIR + "/demux.qza"
    shell:
        """
        qiime tools import \
        --type 'SampleData[PairedEndSequencesWithQuality]' \
        --input-path {input} \
        --output-path {output} \
        --input-format PairedEndFastqManifestPhred33
        """

rule get_demux_qza_unpaired:
    input: 
        PROJECT_DIR + "/manifest.csv"
    output:
        QIIME_OUTPUT_DIR + "/demux.qza"
    shell:
        """
        qiime tools import \
        --type 'SampleData[SequencesWithQuality]' \
        --input-path {input} \
        --output-path {output} \
        --input-format SingleEndFastqManifestPhred33
        """
